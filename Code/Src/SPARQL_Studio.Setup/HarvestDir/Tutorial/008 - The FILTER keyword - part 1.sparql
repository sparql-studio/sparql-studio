# 008 - The FILTER keyword - part 1
# ---------------------------------
# 
# We have seen as, in a simple SPARQL query, you can specify a number of
# triple patterns and you get as a result the requested variables from all
# the triples that match the given patterns.
# 
# This approach may often return more results than we would like -- as in the
# previous case with country names where adding an additional triple pattern 
# tyo the query actually increases the number of valid results, because one 
# country has typically more names (one per language).
# 
# In order to make a further selection on a query result, SPARQL provides the
# FILTER query.
# 
# A filter has the format FILTER <constraint> and will usually appear in a 
# SELECT statement after all triple patterns -- although it doesn't have to.
# 
# Here's an example that will solve our little problem with country names in
# different languages:
# 

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX yago: <http://dbpedia.org/class/yago/>

SELECT ?country ?countryName WHERE 
{
    ?country rdf:type yago:LandlockedCountries ;
    		 rdf:type dbo:Country .
   	?country rdfs:label ?countryName .
   	
   	FILTER (LANG(?countryName) = "en") .
}

